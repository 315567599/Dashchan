import org.gradle.internal.os.OperatingSystem

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:2.1.0'
	}
}

allprojects {
	repositories {
		jcenter()
	}
}

apply plugin: 'com.android.application'

android {
	compileSdkVersion 23
	buildToolsVersion "23.0.3"

	sourceSets.main {
		manifest.srcFile 'AndroidManifest.xml'
		java.srcDirs = ['src', 'src-api', 'src-external']
		resources.srcDirs = ['src']
		aidl.srcDirs = ['src']
		renderscript.srcDirs = ['src']
		res.srcDirs = ['res']
		assets.srcDirs = ['assets']
		jni.srcDirs = []
		jniLibs.srcDir 'libs'
	}

	if (new File("keystore.properties").exists()) {
		Properties properties = new Properties()
		properties.load(new FileInputStream(file("keystore.properties")))

		signingConfigs {
			debug {
				storeFile file(properties['debug.storeFile'])
				storePassword properties['debug.storePassword']
				keyAlias properties['debug.keyAlias']
				keyPassword properties['debug.keyPassword']
			}

			release {
				storeFile file(properties['release.storeFile'])
				storePassword properties['release.storePassword']
				keyAlias properties['release.keyAlias']
				keyPassword properties['release.keyPassword']
			}
		}

		buildTypes.debug.signingConfig signingConfigs.debug
		buildTypes.release.signingConfig signingConfigs.release
	}

	buildTypes {
		release {
			minifyEnabled false
		}
	}

	lintOptions {
		abortOnError false
		disable 'MissingTranslation', 'ValidFragment'
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}
}

def getNdkBuildCommand() {
	def ndkDir = System.env.ANDROID_NDK_HOME
	if (ndkDir == null) {
		Properties properties = new Properties()
		properties.load(new FileInputStream(file("local.properties")))
		ndkDir = properties.getProperty("ndk.dir")
	}
	def windows = OperatingSystem.current().isWindows()
	return (ndkDir != null ? ndkDir + java.io.File.separator : '') + 'ndk-build' + (windows ? '.cmd' : '')
}

task ndkBuildCompile(type: Exec) {
	commandLine getNdkBuildCommand(), '-C', file('.').absolutePath
}

task ndkBuildClean(type: Exec) {
	commandLine getNdkBuildCommand(), 'clean', file('.').absolutePath
}

tasks.withType(JavaCompile) { compileTask ->
	compileTask.dependsOn ndkBuildCompile
}

clean.dependsOn ndkBuildClean

dependencies {
	provided 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
}